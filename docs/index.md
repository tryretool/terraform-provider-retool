---
page_title: "Provider: Retool"
description: |-
  The Retool provider can be used to interact with 
  [Retool Platform API](https://docs.retool.com/org-users/quickstart#retool-api) to configure your Retool instance and Spaces.
---

# Retool Provider

Retool Terraform provider helps you configure Spaces, SSO, Source Control, permissions and other settings on 
self-hosted Retool instance or Retool Cloud organization using [Retool Platform API](https://docs.retool.com/org-users/quickstart#retool-api).

## Prerequisites
Your [Retool plan](https://retool.com/pricing) should support Platform APIs. You'll need to create an API token on **Settings > Retool API** .
The access token should have the following scopes:
- source_control:read, source_control:write
- groups:read, groups:write
- spaces:read, spaces:write
- folders:read, folders:write
- permissions:all:read, permissions:all:write
- configuration_variables:read, configuration_variables:write


## Example Usage

```terraform
provider "retool" {
  host         = "your-retool-instance.com"
  scheme       = "https"
  access_token = "your-access-token"
}


# Create an app folder
resource "retool_folder" "production_apps" {
  name        = "Production Apps"
  folder_type = "app"
}

# Create a permission group
resource "retool_group" "production_apps_users" {
  name = "Production Apps Users"
}

# Grant "use" permissions on the folder to the permission group
resource "retool_permissions" "production_apps_users" {
  subject = {
    type = "group"
    id   = retool_group.production_apps_users.id
  }
  permissions = [
    {
      object = {
        type = "folder"
        id   = retool_folder.production_apps.id
      }
      access_level = "use"
    }
  ]
}
```

~> Hard-coding credentials into any Terraform configuration is not recommended, and risks secret leakage should this
file ever be committed to a public version control system. See [Environment Variables](#environment-variables) for a
better alternative.

<!-- schema generated by tfplugindocs -->
## Schema

### Optional

- `access_token` (String, Sensitive) The access token for the Retool API
- `host` (String) The host of the Retool instance, organization or Space, e.g. 'example.retool.com'
- `requests_per_minute` (Number) The number of requests per minute to allow to the Retool API. Set to 45 by default. Set to -1 to disable rate limiting.
- `scheme` (String) The scheme of the Retool instance, e.g. 'https'

## Environment Variables

You can provide your credentials via the `RETOOL_HOST`, `RETOOL_SCHEME` and `RETOOL_ACCESS_TOKEN`
environment variables, respectively.

```terraform
provider "retool" {
}


# Create an app folder
resource "retool_folder" "production_apps" {
  name        = "Production Apps"
  folder_type = "app"
}

# Create a permission group
resource "retool_group" "production_apps_users" {
  name = "Production Apps Users"
}

# Grant "use" permissions on the folder to the permission group
resource "retool_permissions" "production_apps_users" {
  subject = {
    type = "group"
    id   = retool_group.production_apps_users.id
  }
  permissions = [
    {
      object = {
        type = "folder"
        id   = retool_folder.production_apps.id
      }
      access_level = "use"
    }
  ]
}
```

### Example Usage

```shell
RETOOL_HOST="your-retool-instance.com" \
RETOOL_SCHEME="https" \
RETOOL_ACCESS_TOKEN="your-access-token" \
terraform plan
```

## Rate limiting
Retool API has rate limits. In order to avoid hitting the rate limits, Retool Terraform provider is configured to limit requests to the API to 45 requests per minute.
This might be too slow for complex Retool configurations with a lot of folders and permission groups. To increase the rate limit, you can do the following:

- Disable rate limiting on your self-hosted Retool instance by setting `DISABLE_RATE_LIMIT` environment variable to `true`.

- Increase the rate limit on your self-hosted Retool instance by setting `API_CALLS_PER_MIN` environment variable higher than its default value of 300.

Once you've increased the rate limit, you can increase the `requests_per_minute` parameter in the provider configuration.

```terraform
provider "retool" {
  requests_per_minute = 100
}
```

Or you can disable rate limiting in the provider completely by setting `requests_per_minute` to `-1`.

```terraform
provider "retool" {
  requests_per_minute = -1
}
```

## Provisioning new Retool instance
If you're using Retool Terraform provider to configure a new Retool instance, and you don't want to manually log in to the instance in order to create API token, you can use the following environment variables
in your Retool instance configuration:
```sh
export CREATE_FIRST_ORG="true" # Will automatically create the first organization on the instance
export ADMIN_USER_EMAIL="dzmitry+admin@retool.com" # Email of the admin user
export ADMIN_API_ACCESS_TOKEN="retool_01j33n67seh459gds2n5xjyrfx" # You can use any random string here
export ADMIN_API_ACCESS_TOKEN_SCOPES="source_control:read,source_control:write,groups:read,groups:write,spaces:read,spaces:write,folders:read,folders:write,permissions:all:read,permissions:all:write,configuration_variables:read,configuration_variables:write" # Scopes for the admin user API token
```
